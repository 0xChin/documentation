---
sidebar_position: 1
id: quickstart
---

import Tabs from '@theme/Tabs';
import TabItem from '@theme/TabItem';

# Quickstart

This quickstart demonstrates how to use `xcall`, a cross-chain primitive mimicking Solidity's low level `call` method.

---

## Setup

Install [Node.js](https://nodejs.dev/en/learn/how-to-install-nodejs/) and use **Node.js v16**. Follow the instructions to install `nvm`, a node version manager, which will make switching versions easier.

Install the latest beta version of Connext contracts package in your project.

```bash npm2yarn
npm install @connext/nxtp-contracts@beta
```

---

## Hello Chain

The `HelloTarget` contract on the destination domain (Mumbai) stores a greeting variable that we want to update. The `HelloSource` contract on the origin domain (Goerli) will use `xcall` to execute the updating function on `HelloTarget`.

`HelloTarget` will require a payment of 1 TEST token to update the greeting. This way, we can see how `xcall` can send assets as well as calldata cross chains.

### Target Contract

All target contracts must implement Connext's `IXReceiver` interface. This interface ensures that Connext can call the contract and pass necessary data. 

```solidity showLineNumbers
pragma solidity ^0.8.15;

import {IXReceiver} from "@connext/nxtp-contracts/contracts/core/connext/interfaces/IXReceiver.sol";

contract HelloTarget is IXReceiver {
  string public greeting;

  // Hardcoded cost to update the greeting, in wei units
  uint256 public cost = 1e18;

  // The TEST Token on Mumbai
  IERC20 public token = IERC20(0xeDb95D8037f769B72AAab41deeC92903A98C9E16);

  /** @notice The receiver function as required by the IXReceiver interface.
    * @dev The Connext bridge contract will call this function.
    */
  function xReceive(
    bytes32 _transferId,
    uint256 _amount,
    address _asset,
    address _originSender,
    uint32 _origin,
    bytes memory _callData
  ) external returns (bytes memory) {
    // Enforce a cost to update the greeting
    require(
      _asset == address(token) && _amount >= cost,
      "Must pay at least 1 TEST"
    );

    _updateGreeting(_callData);
  }

  /** @notice Internal function to update the greeting.
    * @param _callData Calldata containing the new greeting.
    */
  function _updateGreeting(bytes memory _callData) internal {
    string memory newGreeting = abi.decode(_callData, (string));
    greeting = newGreeting;
  }
}
```

The goal is to call `_updateGreeting` from the origin domain.

### Source Contract

The source contract initiates the cross-chain operation with `xcall` and passes the encoded greeting into the call. All `xcall` params are detailed [here](./reference/contracts/calls#xcall).

Notice the token that is passed in is the TEST token on Goerli. You can read more about token flavors and why they matter [here](./reference/faq#what-are-token-flavors).

```solidity showLineNumbers
pragma solidity ^0.8.15;

import {IConnext} from "@connext/nxtp-contracts/contracts/core/connext/interfaces/IConnext.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";

/**
 * @title HelloSource
 * @notice Example source contract that updates a greeting in HelloTarget.
 * @dev Must pay at least 1 TEST to update the greeting.
 */
contract HelloSource {
  // The connext contract on the origin domain
  IConnext public immutable connext;

  // Hardcoded cost to update the greeting, in wei units
  uint256 public cost = 1e18;

  // The canonical TEST Token on Goerli
  IERC20 public token = IERC20(0x7ea6eA49B0b0Ae9c5db7907d139D9Cd3439862a1);

  constructor(IConnext _connext) {
    connext = _connext;
  }

  /** @notice Updates a greeting variable on the HelloTarget contract.
    * @param target Address of the HelloTarget contract.
    * @param destinationDomain The destination domain ID.
    * @param newGreeting New greeting to update to.
    * @param relayerFee The fee offered to relayers. On testnet, this can be 0.
    */
  function updateGreeting (
    address target, 
    uint32 destinationDomain,
    string memory newGreeting,
    uint256 relayerFee
  ) external payable {
    require(
      token.allowance(msg.sender, address(this)) >= cost,
      "User must approve amount"
    );

    // User sends funds to this contract
    token.transferFrom(msg.sender, address(this), cost);

    // This contract approves transfer to Connext
    token.approve(address(connext), cost);

    // Encode the data needed for the target contract call.
    bytes memory callData = abi.encode(newGreeting);

    connext.xcall{value: relayerFee}(
      destinationDomain, // _destination: Domain ID of the destination chain
      target,            // _to: address of the target contract
      address(token),    // _asset: address of the token contract
      msg.sender,        // _delegate: address that can revert or forceLocal on destination
      cost,              // _amount: amount of tokens to transfer
      30,                // _slippage: the maximum amount of slippage the user will accept in BPS, in this case 0.3%
      callData           // _callData: the encoded calldata to send
    );
  }
}
```

Since funds are going to be routed through Connext's contracts, the user must first approve a spending allowance of the TEST ERC20 to the `HelloSource` contract. The `require` clause starting on line 37 checks for this allowance. 

You can use [Etherscan and write](https://goerli.etherscan.io/address/0x7ea6eA49B0b0Ae9c5db7907d139D9Cd3439862a1#writeContract) to the token's `approve` function to do this approval: 

<img src="/img/developers/TestERC20_Etherscan_Approve.png" alt="TestERC20 Etherscan Approve" width="400"/>

The tokens will move from `User's wallet` => `HelloSource` => `Connext` => `HelloTarget`.

---

## Next Steps

- Try [tracking the status](./guides/xcall-status) of an `xcall` after you send it.
- Learn about [authentication](./guides/authentication) and important security considerations.
- See how [nested xcalls](./guides/nested-xcalls) can open up infinite cross-chain possibilities.
- Fork the xApp Starter Kit below and build your own xApp.

[xApp Starter Kit](https://github.com/connext/xapp-starter/)
